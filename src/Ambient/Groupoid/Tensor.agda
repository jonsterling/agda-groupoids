{-# OPTIONS --without-K #-}

module Ambient.Groupoid.Tensor where

open import Agda.Primitive
import Ambient.Groupoid.Base as G
open import Ambient.Groupoid.Map as Map
open import Ambient.Groupoid.Tensor.Boot public
import Setoid as S
open import Type as T

infix 0 РЪе_,Рѓѓ,Рѓђ_РЪЕ
infix 0 РЪе_РіЌРѓѓ,Рѓђ_РЪЕ

¤ђРѓђ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ (A РіЌ B) Map.РЄњРѓђрхЌ A
_$Рѓђ_ ¤ђРѓђ =
  ¤ђРЂ░Рѓђ
-$РѓЂ╦б- ¤ђРѓђ =
  S.Ten.¤ђРѓђ
idn (¤ђРѓђ {A = A}) =
  ╬╗ {a} Рєњ S.idn (G.hom╦б A (¤ђРЂ░Рѓђ a , ¤ђРЂ░Рѓђ a)) _
cmp (¤ђРѓђ {A = A}) =
  ╬╗ {a}{_}{c} _ _ Рєњ S.idn (G.hom╦б A (¤ђРЂ░Рѓђ a , ¤ђРЂ░Рѓђ c)) _

¤ђРѓЂ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ (A РіЌ B) Map.РЄњРѓђрхЌ B
_$Рѓђ_ ¤ђРѓЂ =
  ¤ђ┬╣Рѓђ
-$РѓЂ╦б- ¤ђРѓЂ =
  S.Ten.¤ђРѓЂ
idn (¤ђРѓЂ {B = B}) =
  ╬╗ {a} Рєњ S.idn (G.hom╦б B (¤ђ┬╣Рѓђ a , ¤ђ┬╣Рѓђ a)) _
cmp (¤ђРѓЂ {B = B}) =
  ╬╗ {a}{_}{c} _ _ Рєњ S.idn (G.hom╦б B (¤ђ┬╣Рѓђ a , ¤ђ┬╣Рѓђ c)) _

РЪе-,-РЪЕ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {X : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ ((X Map.РЄњРѓђрхЇ A) РіЌ (X Map.РЄњРѓђрхЇ B)) Map.РЄњРѓђрхЌ (X Map.РЄњРѓђрхЇ A РіЌ B)
_$Рѓђ_ (_$Рѓђ_ РЪе-,-РЪЕ FG) =
  РЪе ¤ђРЂ░Рѓђ FG $Рѓђ_ ,Рѓђ ¤ђ┬╣Рѓђ FG $Рѓђ_ РЪЕ
-$РѓЂ╦б- (_$Рѓђ_ РЪе-,-РЪЕ FG) =
  S.Ten.РЪе-,-РЪЕ S.Map.$Рѓђ (-$РѓЂ╦б- (¤ђРЂ░Рѓђ FG) , -$РѓЂ╦б- (¤ђ┬╣Рѓђ FG))
idn (_$Рѓђ_ РЪе-,-РЪЕ (F , G)) =
  Map.idn F , Map.idn G
cmp (_$Рѓђ_ РЪе-,-РЪЕ (F , G)) _ _ =
  cmp F _ _ , cmp G _ _
Map.comРѓЂ (S.Map._$Рѓђ_ (-$РѓЂ╦б- РЪе-,-РЪЕ) (╬▒ , ╬▓)) =
  Map.comРѓЂ ╬▒ , Map.comРѓЂ ╬▓
Map.natРѓЂ (S.Map._$Рѓђ_ (-$РѓЂ╦б- РЪе-,-РЪЕ) (╬▒ , ╬▓)) _ =
  Map.natРѓЂ ╬▒ _ , Map.natРѓЂ ╬▓ _
Map.comРѓѓ (S.Map._$РѓЂ_ (-$РѓЂ╦б- РЪе-,-РЪЕ) (╬▒┬╣ , ╬▓┬╣)) =
  Map.comРѓѓ ╬▒┬╣ , Map.comРѓѓ ╬▓┬╣
Map.comРѓѓ (idn (РЪе-,-РЪЕ {A = A}{B})) =
  S.idn (G.hom╦б A _) _ , S.idn (G.hom╦б B _) _
Map.comРѓѓ (cmp (РЪе-,-РЪЕ {A = A}{B}) _ _) =
  S.idn (G.hom╦б A _) _ , S.idn (G.hom╦б B _) _

РЪе-РіЌ-РЪЕ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░ РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ {XРѓђ : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {XРѓЂ : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ ((XРѓђ Map.РЄњРѓђрхЇ A) РіЌ (XРѓЂ Map.РЄњРѓђрхЇ B)) Map.РЄњРѓђрхЌ ((XРѓђ РіЌ XРѓЂ) Map.РЄњРѓђрхЇ (A РіЌ B))
_$Рѓђ_ (_$Рѓђ_ РЪе-РіЌ-РЪЕ FG) =
  РЪе ¤ђРЂ░Рѓђ FG $Рѓђ_ ├ЌРѓђ ¤ђ┬╣Рѓђ FG $Рѓђ_ РЪЕ
-$РѓЂ╦б- (_$Рѓђ_ РЪе-РіЌ-РЪЕ FG) =
  S.Ten.РЪе-РіЌ-РЪЕ S.Map.$Рѓђ (-$РѓЂ╦б- (¤ђРЂ░Рѓђ FG) , -$РѓЂ╦б- (¤ђ┬╣Рѓђ FG))
idn (_$Рѓђ_ РЪе-РіЌ-РЪЕ (F , G)) =
  Map.idn F , Map.idn G
cmp (_$Рѓђ_ РЪе-РіЌ-РЪЕ (F , G)) _ _ =
  Map.cmp F _ _ , Map.cmp G _ _
Map.comРѓЂ (S.Map._$Рѓђ_ (-$РѓЂ╦б- РЪе-РіЌ-РЪЕ) (╬▒ , ╬▓)) =
  Map.comРѓЂ ╬▒ , Map.comРѓЂ ╬▓
Map.natРѓЂ (S.Map._$Рѓђ_ (-$РѓЂ╦б- РЪе-РіЌ-РЪЕ) (╬▒ , ╬▓)) _ =
  Map.natРѓЂ ╬▒ _ , Map.natРѓЂ ╬▓ _
Map.comРѓѓ (S.Map._$РѓЂ_ (-$РѓЂ╦б- РЪе-РіЌ-РЪЕ) (╬▒┬╣ , ╬▓┬╣)) =
  Map.comРѓѓ ╬▒┬╣ , Map.comРѓѓ ╬▓┬╣
Map.comРѓѓ (idn (РЪе-РіЌ-РЪЕ {A = A}{B})) =
  S.idn (G.hom╦б A _) _ , S.idn (G.hom╦б B _) _
Map.comРѓѓ (cmp (РЪе-РіЌ-РЪЕ {A = A}{B}) _ _) =
  S.idn (G.hom╦б A _) _ , S.idn (G.hom╦б B _) _

╬▒ : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ (A РіЌ B) РіЌ C Map.РЄњРѓђрхЌ A РіЌ (B РіЌ C)
Map._$Рѓђ_ ╬▒ ((a , b) , c) =
  a , (b , c)
S.Map._$Рѓђ_ (Map.-$РѓЂ╦б- ╬▒) ((f , g) , h) =
  f , (g , h)
S.Map._$РѓЂ_ (Map.-$РѓЂ╦б- ╬▒) ((p , q) , r) =
  p , (q , r)
Map.idn (╬▒ {A = A}{B}{C}) =
  S.idn (G.hom╦б A _) _ , (S.idn (G.hom╦б B _) _ , S.idn (G.hom╦б C _) _)
Map.cmp (╬▒ {A = A}{B}{C}) _ _ =
  S.idn (G.hom╦б A _) _ , (S.idn (G.hom╦б B _) _ , S.idn (G.hom╦б C _) _)

РЪе_,Рѓѓ,Рѓђ_РЪЕ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {X : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ (F : X Map.РЄњРѓђрхЌ A)
  Рєњ (G : X Map.РЄњРѓђрхЌ B)
  Рєњ (X Map.РЄњРѓђрхЌ A РіЌ B)
РЪе F ,Рѓѓ,Рѓђ G РЪЕ = РЪе-,-РЪЕ Map.$Рѓђ (F , G)

РЪе_РіЌРѓѓ,Рѓђ_РЪЕ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░ РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ {XРѓђ : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {XРѓЂ : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A  : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {B  : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ (F  : XРѓђ Map.РЄњРѓђрхЌ A)
  Рєњ (G  : XРѓЂ Map.РЄњРѓђрхЌ B)
  Рєњ ((XРѓђ РіЌ XРѓЂ) Map.РЄњРѓђрхЌ (A РіЌ B))
РЪе F РіЌРѓѓ,Рѓђ G РЪЕ = РЪе-РіЌ-РЪЕ Map.$Рѓђ (F , G)

РЪе_[_],Рѓђ_РЪЕ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░ РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ {X : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {R : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ (F : X Map.РЄњРѓђрхЌ A)
  Рєњ (K : A РіЌ B Map.РЄњРѓђрхЌ R)
  Рєњ (G : X Map.РЄњРѓђрхЌ B)
  Рєњ (X Map.РЄњРѓђрхЌ R)
РЪе F [ K ],Рѓђ G РЪЕ = K РѕўРѓђрхЌ РЪе-,-РЪЕ Map.$Рѓђ (F , G)

РЪе_[_]РіЌРѓђ_РЪЕ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░ РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░ РёЊРѓёрхњ РёЊРѓё╦брхњ РёЊРѓё╦б╩░}
  Рєњ {XРѓђ : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {XРѓЂ : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A  : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {B  : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЃрхњ РёЊРѓЃ╦брхњ РёЊРѓЃ╦б╩░}
  Рєњ {R  : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓёрхњ РёЊРѓё╦брхњ РёЊРѓё╦б╩░}
  Рєњ (F  : XРѓђ Map.РЄњРѓђрхЌ A)
  Рєњ (K  : A РіЌ B Map.РЄњРѓђрхЌ R)
  Рєњ (G  : XРѓЂ Map.РЄњРѓђрхЌ B)
  Рєњ ((XРѓђ РіЌ XРѓЂ) Map.РЄњРѓђрхЌ R)
РЪе F [ K ]РіЌРѓђ G РЪЕ = K РѕўРѓђрхЌ РЪе-РіЌ-РЪЕ Map.$Рѓђ (F , G)

-- ap-lhsРѓђ
--   : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
--   Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
--   Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
--   Рєњ {C : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
--   Рєњ (F : A РіЌ B Map.РЄњРѓђрхЌ C)
--   Рєњ (a : G.obj A)
--   Рєњ B Map.РЄњРѓђрхЌ C
-- Map._$Рѓђ_ (ap-lhsРѓђ F a) =
--   T.Ten.ap-lhs (F Map.$Рѓђ_) a
-- Map.-$РѓЂ╦б- (ap-lhsРѓђ {A = A} F a) =
--   S.Ten.ap-lhsРѓђ (Map.-$РѓЂ╦б- F) (G.idn╦б A S.Map.$Рѓђ _)
-- Map.idn (ap-lhsРѓђ F a) =
--   Map.idn F
-- Map.cmp (ap-lhsРѓђ {A = A}{B}{C} F a) g f =
--   S.cmpрхЌ (G.hom╦б C _)
--     ( Map.cmp F _ _
--     , F Map.$Рѓѓ
--       ( S.invрхЌ (G.hom╦б A _) (G.idn-rhs A _)
--       , S.idnрхЌ (G.hom╦б B _) _) )

-- ap-rhsРѓђ
--   : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
--   Рєњ {A : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
--   Рєњ {B : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
--   Рєњ {C : G.­ЮћіРѓѓ,Рѓђ d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
--   Рєњ (F : A РіЌ B Map.РЄњРѓђрхЌ C)
--   Рєњ (b : G.obj B)
--   Рєњ A Map.РЄњРѓђрхЌ C
-- Map._$Рѓђ_ (ap-rhsРѓђ F b) =
--   T.Ten.ap-rhs (F Map.$Рѓђ_) b
-- Map.-$РѓЂ╦б- (ap-rhsРѓђ {B = B} F b) =
--   S.Ten.ap-rhsРѓђ (Map.-$РѓЂ╦б- F) (G.idn╦б B S.Map.$Рѓђ _)
-- Map.idn (ap-rhsРѓђ F b) =
--   Map.idn F
-- Map.cmp (ap-rhsРѓђ {A = A}{B}{C} F b) g f =
--   S.cmpрхЌ (G.hom╦б C _)
--     ( Map.cmp F _ _
--     , F Map.$Рѓѓ
--         ( S.idnрхЌ (G.hom╦б A _) _
--         , S.invрхЌ (G.hom╦б B _) (G.idn-rhs B _)) )
