{-# OPTIONS --without-K #-}

module Ambient.Groupoid.Map where

open import Agda.Primitive
import Ambient.Groupoid.Base as G
import Ambient.Groupoid.Map.Boot as Map
import Ambient.Groupoid.Tensor.Boot as Ten
import Ambient.Groupoid.Terminal as ­ЮЪЎ
import Setoid as S
open import Type as T
  using (_,_)

infixr 2 _РЄњРѓђрхЇ_
infixr 0 _РЄњРѓЂрхЌ_
infix 0 _РЄћРѓЂрхЌ_

record _РЄњРѓЂрхЌ_
  {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  (F G : A Map.РЄњРѓђрхЌ B)
    : Set ((РёЊРѓђрхњ Ріћ РёЊРѓђ╦брхњ) Ріћ (РёЊРѓЂ╦брхњ Ріћ РёЊРѓЂ╦б╩░)) where
  no-eta-equality
  field
    comРѓЂ
      : Рѕђ {a}
      Рєњ S.obj (G.hom╦б B (F Map.$Рѓђ a , G Map.$Рѓђ a))
  field
    .natРѓЂ
      : Рѕђ {a b}
      Рєњ (f : S.obj (G.hom╦б A (a , b)))
      Рєњ S.homрхЌ (G.hom╦б B (F Map.$Рѓђ _ , G Map.$Рѓђ _))
          ( G.cmp╦б B S.Map.$Рѓђ (comРѓЂ , F Map.$РѓЂ f)
          , G.cmp╦б B S.Map.$Рѓђ (G Map.$РѓЂ f , comРѓЂ)
          )
open _РЄњРѓЂрхЌ_ public

record _РЄњРѓѓ_
  {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  {F G : A Map.РЄњРѓђрхЌ B}
  (╬▒ ╬▓ : F РЄњРѓЂрхЌ G)
    : Set (РёЊРѓђрхњ Ріћ (РёЊРѓЂ╦брхњ Ріћ РёЊРѓЂ╦б╩░)) where
  no-eta-equality
  field
    .comРѓѓ
      : Рѕђ {a}
      Рєњ S.homрхЌ (G.hom╦б B (F Map.$Рѓђ a , G Map.$Рѓђ a)) (comРѓЂ ╬▒ {a} , comРѓЂ ╬▓ {a})
open _РЄњРѓѓ_ public

_РЄњРѓЂ╦б_
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ (F G : A Map.РЄњРѓђрхЌ B)
  Рєњ S.t S.Dir.РЅѕ _ _
S.obj (_РЄњРѓЂ╦б_ F G) =
  F РЄњРѓЂрхЌ G
S.homрхЌ (_РЄњРѓЂ╦б_ F G) =
  ╬╗ {(╬▒ , ╬▓) Рєњ ╬▒ РЄњРѓѓ ╬▓}
comРѓѓ (S.idnрхЌ (_РЄњРѓЂ╦б_ {B = B} F G) _) =
  S.idnрхЌ (G.hom╦б B (F Map.$Рѓђ _ , G Map.$Рѓђ _)) _
comРѓѓ (S.cmpрхЌ (_РЄњРѓЂ╦б_ {B = B} F G) (gРЄњРѓѓ , fРЄњРѓѓ)) =
  S.cmpрхЌ (G.hom╦б B (F Map.$Рѓђ _ , G Map.$Рѓђ _))
    (comРѓѓ gРЄњРѓѓ , comРѓѓ fРЄњРѓѓ)
comРѓѓ (S.invрхЌ (_РЄњРѓЂ╦б_ {B = B} F G) fРЄњРѓѓ) =
  S.invрхЌ (G.hom╦б B (F Map.$Рѓђ _ , G Map.$Рѓђ _))
    (comРѓѓ fРЄњРѓѓ)

idnРѓЂ╦б
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ (F : A Map.РЄњРѓђрхЌ B)
  Рєњ S.­ЮЪЎ.sРЂ░ S.Map.РЄњРѓђрхЌ (F РЄњРѓЂ╦б F)
comРѓЂ (S.Map._$Рѓђ_ (idnРѓЂ╦б {B = B} F) _) =
  G.idn╦б B S.Map.$Рѓђ _
natРѓЂ (S.Map._$Рѓђ_ (idnРѓЂ╦б {B = B} F) _) _ =
  S.cmpрхЌ (G.hom╦б B (F Map.$Рѓђ _ , F Map.$Рѓђ _))
    ( S.invрхЌ (G.hom╦б B _) (G.idn-rhs B (F Map.$РѓЂ _))
    , G.idn-lhs B (F Map.$РѓЂ _) )
comРѓѓ (S.Map._$РѓЂ_ (idnРѓЂ╦б {B = B} F) _) =
  G.idn╦б B S.Map.$РѓЂ _

cmpРѓЂ╦б
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {F G H : A Map.РЄњРѓђрхЌ B}
  Рєњ ((G РЄњРѓЂ╦б H) S.Ten.РіЌ (F РЄњРѓЂ╦б G)) S.Map.РЄњРѓђрхЌ (F РЄњРѓЂ╦б H)
comРѓЂ (S.Map._$Рѓђ_ (cmpРѓЂ╦б {B = B}) (╬▓ , ╬▒)) =
  G.cmp╦б B S.Map.$Рѓђ (comРѓЂ ╬▓ , comРѓЂ ╬▒)
natРѓЂ (S.Map._$Рѓђ_ (cmpРѓЂ╦б {B = B}) (╬▓ , ╬▒)) _ =
  S.cmpрхЌ (G.hom╦б B _)
  ( G.cmp-ass B _ _ _
  , S.cmpрхЌ (G.hom╦б B _)
    ( G.cmp╦б B S.Map.$РѓЂ
      ( natРѓЂ ╬▓ _
      , S.idnрхЌ (G.hom╦б B _) _ )
    , S.cmpрхЌ (G.hom╦б B _)
      ( S.invрхЌ (G.hom╦б B _) (G.cmp-ass B _ _ _)
      , S.cmpрхЌ (G.hom╦б B _)
        ( G.cmp╦б B S.Map.$РѓЂ
          ( S.idnрхЌ (G.hom╦б B _) _
          , natРѓЂ ╬▒ _ )
        , G.cmp-ass B _ _ _ ))))
comРѓѓ (S.Map._$РѓЂ_ (cmpРѓЂ╦б {B = B}) (╬▓ , ╬▒)) =
  G.cmp╦б B S.Map.$РѓЂ (comРѓѓ ╬▓ , comРѓѓ ╬▒)

invРѓЂ╦б
  : Рѕђ ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.t S.Dir.РЅѕ РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t S.Dir.РЅѕ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {F G : A Map.РЄњРѓђрхЌ B}
  Рєњ (F РЄњРѓЂ╦б G) S.Map.РЄњРѓђрхЌ (G РЄњРѓЂ╦б F)
comРѓЂ (S.Map._$Рѓђ_ (invРѓЂ╦б {B = B}) ╬▒) =
  G.inv╦б B S.Map.$Рѓђ (comРѓЂ ╬▒)
comРѓѓ (S.Map._$РѓЂ_ (invРѓЂ╦б {B = B}) ╬▒) =
  G.inv╦б B S.Map.$РѓЂ (comРѓѓ ╬▒)
natРѓЂ (S.Map._$Рѓђ_ (invРѓЂ╦б {A = A}{B = B}) ╬▒) _ =
  S.cmpрхЌ (G.hom╦б B _)
    ( S.cmpрхЌ (G.hom╦б B _)
      ( S.cmpрхЌ (G.hom╦б B _)
        ( S.cmpрхЌ (G.hom╦б B _)
          ( S.cmpрхЌ (G.hom╦б B _)
            ( G.cmp╦б B S.Map.$РѓЂ
              ( S.cmpрхЌ (G.hom╦б B _)
                ( S.cmpрхЌ (G.hom╦б B _)
                  ( S.cmpрхЌ (G.hom╦б B _)
                    ( G.idn-lhs B _
                    , G.cmp╦б B S.Map.$РѓЂ
                      ( G.inv-lhs B _
                      , S.idnрхЌ (G.hom╦б B _) _ ) )
                  , S.invрхЌ (G.hom╦б B _) (G.cmp-ass B _ _ _) )
                , G.cmp╦б B S.Map.$РѓЂ
                  ( S.idnрхЌ (G.hom╦б B _) _
                  , S.invрхЌ (G.hom╦б B _) (natРѓЂ ╬▒ _) ))
              , S.idnрхЌ (G.hom╦б B _) _ )
            , S.invрхЌ (G.hom╦б B _) (G.cmp-ass B _ _ _) )
          , G.cmp╦б B S.Map.$РѓЂ
            ( S.idnрхЌ (G.hom╦б B _) _
            , S.invрхЌ (G.hom╦б B _) (G.cmp-ass B _ _ _) ) )
        , G.cmp-ass B _ _ _)
      , G.cmp╦б B S.Map.$РѓЂ (S.idnрхЌ (G.hom╦б B _) _ , G.inv-rhs B _))
    , S.invрхЌ (G.hom╦б B _) (G.idn-rhs B _) )

record _РЄћРѓЂрхЌ_
  {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  (F G : A Map.РЄњРѓђрхЌ B)
    : Set ((РёЊРѓђрхњ Ріћ РёЊРѓђ╦брхњ) Ріћ (РёЊРѓЂ╦брхњ Ріћ РёЊРѓЂ╦б╩░)) where
  no-eta-equality
  field
    fwd : F РЄњРѓЂрхЌ G
    bwd : G РЄњРѓЂрхЌ F
  field
    .iso-fwd : S.homрхЌ (F РЄњРѓЂ╦б F) (cmpРѓЂ╦б S.Map.$Рѓђ (bwd , fwd) , idnРѓЂ╦б F S.Map.$Рѓђ _)
    .iso-bwd : S.homрхЌ (G РЄњРѓЂ╦б G) (cmpРѓЂ╦б S.Map.$Рѓђ (fwd , bwd) , idnРѓЂ╦б G S.Map.$Рѓђ _)

  РЪе_РЄћРЪЕРѓЂ : Рѕђ {a} Рєњ G.homРѓђ B (F Map.$Рѓђ a) (G Map.$Рѓђ a)
  РЪе_РЄћРЪЕРѓЂ = comРѓЂ fwd

  РЪеРЄћ_РЪЕРѓЂ : Рѕђ {a} Рєњ G.homРѓђ B (G Map.$Рѓђ a) (F Map.$Рѓђ a)
  РЪеРЄћ_РЪЕРѓЂ = comРѓЂ bwd
open _РЄћРѓЂрхЌ_ public

-- FIXME: cmp-wРѓђ and cmp-wРѓђ are problematic because of H╬▒/╬▓F dependency

cmpРѓЂрхЌ-wРѓђ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.t d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {F G : A Map.РЄњРѓђрхЌ B}
  Рєњ (H╬▒ : (B Map.РЄњРѓђрхЌ C) T.Ten.РіЌ (F РЄњРѓЂрхЌ G))
  Рєњ (T.Ten.¤ђРѓђ H╬▒ Map.РѕўРѓђрхЌ F) РЄњРѓЂрхЌ (T.Ten.¤ђРѓђ H╬▒ Map.РѕўРѓђрхЌ G)
comРѓЂ (cmpРѓЂрхЌ-wРѓђ (H , ╬▒)) =
  H Map.$РѓЂ comРѓЂ ╬▒
natРѓЂ (cmpРѓЂрхЌ-wРѓђ {C = C} (H , ╬▒)) f =
  S.cmpрхЌ (G.hom╦б C _)
  ( Map.cmp H _ _
  , S.cmpрхЌ (G.hom╦б C _)
    ( H Map.$Рѓѓ (natРѓЂ ╬▒ f)
    , S.invрхЌ (G.hom╦б C _) (Map.cmp H _ _)))

cmpРѓЂрхЌ-wРѓЂ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.t d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {G H : B Map.РЄњРѓђрхЌ C}
  Рєњ (╬▓F : (G РЄњРѓЂрхЌ H) T.Ten.РіЌ (A Map.РЄњРѓђрхЌ B))
  Рєњ (G Map.РѕўРѓђрхЌ T.Ten.¤ђРѓЂ ╬▓F) РЄњРѓЂрхЌ (H Map.РѕўРѓђрхЌ T.Ten.¤ђРѓЂ ╬▓F)
comРѓЂ (cmpРѓЂрхЌ-wРѓЂ (╬▓ , F)) = comРѓЂ ╬▓
natРѓЂ (cmpРѓЂрхЌ-wРѓЂ (╬▓ , F)) f = natРѓЂ ╬▓ (F Map.$РѓЂ f)

cmpРѓђ╦б-hРѓђ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.t d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {F G : A Map.РЄњРѓђрхЌ B}
  Рєњ {H K : B Map.РЄњРѓђрхЌ C}
  Рєњ ((H РЄњРѓЂ╦б K) S.Ten.РіЌ (F РЄњРѓЂ╦б G)) S.Map.РЄњРѓђрхЌ ((H Map.РѕўРѓђрхЌ F) РЄњРѓЂ╦б (K Map.РѕўРѓђрхЌ G))
comРѓЂ (S.Map._$Рѓђ_ (cmpРѓђ╦б-hРѓђ {C = C} {K = K}) (╬▓ , ╬▒)) =
  G.cmp╦б C S.Map.$Рѓђ (K Map.$РѓЂ comРѓЂ ╬▒ , comРѓЂ ╬▓)
comРѓѓ (S.Map._$РѓЂ_ (cmpРѓђ╦б-hРѓђ {C = C} {K = K}) (╬▓ , ╬▒)) =
  G.cmp╦б C S.Map.$РѓЂ (K Map.$Рѓѓ comРѓѓ ╬▒ , comРѓѓ ╬▓)
natРѓЂ (S.Map._$Рѓђ_ (cmpРѓђ╦б-hРѓђ {C = C}{K = K}) (╬▓ , ╬▒)) _ =
  S.cmpрхЌ (G.hom╦б C _)
    ( S.cmpрхЌ (G.hom╦б C _)
      ( S.cmpрхЌ (G.hom╦б C _)
        ( S.cmpрхЌ (G.hom╦б C _)
          ( G.cmp-ass C _ _ _
          , G.cmp╦б C S.Map.$РѓЂ
            ( S.cmpрхЌ (G.hom╦б C _)
              ( S.cmpрхЌ (G.hom╦б C _)
                ( Map.cmp K _ _
                , K Map.$Рѓѓ natРѓЂ ╬▒ _ )
              , S.invрхЌ (G.hom╦б C _) (Map.cmp K _ _) )
            , S.idnрхЌ (G.hom╦б C _) _) )
        , S.invрхЌ (G.hom╦б C _) (G.cmp-ass C _ _ _) )
      , G.cmp╦б C S.Map.$РѓЂ (S.idnрхЌ (G.hom╦б C _) _ , natРѓЂ ╬▓ _) )
    , G.cmp-ass C _ _ _ )

cmpРѓђ╦б-hРѓЂ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.t d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {F G : A Map.РЄњРѓђрхЌ B}
  Рєњ {H K : B Map.РЄњРѓђрхЌ C}
  Рєњ ((H РЄњРѓЂ╦б K) S.Ten.РіЌ (F РЄњРѓЂ╦б G)) S.Map.РЄњРѓђрхЌ ((H Map.РѕўРѓђрхЌ F) РЄњРѓЂ╦б (K Map.РѕўРѓђрхЌ G))
comРѓЂ (S.Map._$Рѓђ_ (cmpРѓђ╦б-hРѓЂ {C = C}{H = H}) (╬▓ , ╬▒)) =
  G.cmp╦б C S.Map.$Рѓђ (comРѓЂ ╬▓ , H Map.$РѓЂ comРѓЂ ╬▒)
comРѓѓ (S.Map._$РѓЂ_ (cmpРѓђ╦б-hРѓЂ {C = C}{H = H}) (╬▓ , ╬▒)) =
  G.cmp╦б C S.Map.$РѓЂ (comРѓѓ ╬▓ , H Map.$Рѓѓ comРѓѓ ╬▒)
natРѓЂ (S.Map._$Рѓђ_ (cmpРѓђ╦б-hРѓЂ {C = C}{H = H}) (╬▓ , ╬▒)) _ =
  S.cmpрхЌ (G.hom╦б C _)
    ( S.cmpрхЌ (G.hom╦б C _)
      ( S.cmpрхЌ (G.hom╦б C _)
        ( S.cmpрхЌ (G.hom╦б C _)
          ( G.cmp-ass C _ _ _
          , G.cmp╦б C S.Map.$РѓЂ
            ( natРѓЂ ╬▓ _
            , S.idnрхЌ (G.hom╦б C _) _ ) )
        , S.invрхЌ (G.hom╦б C _) (G.cmp-ass C _ _ _) )
      , G.cmp╦б C S.Map.$РѓЂ
        ( S.idnрхЌ (G.hom╦б C _) _
        , S.cmpрхЌ (G.hom╦б C _)
          ( S.cmpрхЌ (G.hom╦б C _)
            ( Map.cmp H _ _
            , H Map.$Рѓѓ natРѓЂ ╬▒ _ )
          , S.invрхЌ (G.hom╦б C _) (Map.cmp H _ _) ) ) )
    , G.cmp-ass C _ _ _ )

_РЄњРѓђрхЇ_ : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ (A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░)
  Рєњ (B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░)
  Рєњ G.t d _ _ _
G.obj (A РЄњРѓђрхЇ B) =
  A Map.РЄњРѓђрхЌ B
G.hom╦б (A РЄњРѓђрхЇ B) =
  ╬╗ {(F , G) Рєњ F РЄњРѓЂ╦б G}
G.idn╦б (A РЄњРѓђрхЇ B) =
  ╬╗ {F} Рєњ idnРѓЂ╦б F
G.cmp╦б (A РЄњРѓђрхЇ B) =
  cmpРѓЂ╦б
G.inv╦б (_РЄњРѓђрхЇ_ {G.Dir.РЅц} A B) =
  _
G.inv╦б (_РЄњРѓђрхЇ_ {G.Dir.РЅѕ} A B) =
  invРѓЂ╦б
comРѓѓ (G.idn-lhs (A РЄњРѓђрхЇ B) ╬▒) =
  G.idn-lhs B (comРѓЂ ╬▒)
comРѓѓ (G.idn-rhs (A РЄњРѓђрхЇ B) ╬▒) =
  G.idn-rhs B (comРѓЂ ╬▒)
comРѓѓ (G.cmp-ass (A РЄњРѓђрхЇ B) ╬▒ ╬▓ ╬│) =
  G.cmp-ass B (comРѓЂ ╬▒) (comРѓЂ ╬▓) (comРѓЂ ╬│)
G.inv-lhs (_РЄњРѓђрхЇ_ {G.Dir.РЅц} A B) =
  _
comРѓѓ (G.inv-lhs (_РЄњРѓђрхЇ_ {G.Dir.РЅѕ} A B) ╬▒) =
  G.inv-lhs B (comРѓЂ ╬▒)
G.inv-rhs (_РЄњРѓђрхЇ_ {G.Dir.РЅц} A B) f =
  _
comРѓѓ (G.inv-rhs (_РЄњРѓђрхЇ_ {G.Dir.РЅѕ} A B) ╬▒) =
  G.inv-rhs B (comРѓЂ ╬▒)

idnРѓђрхЇ
  : Рѕђ {d} ..{РёЊрхњ РёЊ╦брхњ РёЊ╦б╩░}
  Рєњ {A : G.t d РёЊрхњ РёЊ╦брхњ РёЊ╦б╩░}
  Рєњ ­ЮЪЎ.g Map.РЄњРѓђрхЌ (A РЄњРѓђрхЇ A)
Map._$Рѓђ_ idnРѓђрхЇ =
  Map.idnРѓђрхЌ
Map.-$РѓЂ╦б- (idnРѓђрхЇ {A = A}) =
  G.idn╦б (A РЄњРѓђрхЇ A)
comРѓѓ (Map.idn (idnРѓђрхЇ {A = A})) =
  S.idnрхЌ (G.hom╦б A _) _
comРѓѓ (Map.cmp (idnРѓђрхЇ {A = A}) g f) =
  S.invрхЌ (G.hom╦б A _) (G.idn-rhs A (G.idn╦б A S.Map.$Рѓђ _))

cmpРѓђрхЇ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.t d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ ((B РЄњРѓђрхЇ C) Ten.РіЌ (A РЄњРѓђрхЇ B)) Map.РЄњРѓђрхЌ (A РЄњРѓђрхЇ C)
Map._$Рѓђ_ cmpРѓђрхЇ =
  Map.cmpРѓђрхЌ
Map.-$РѓЂ╦б- cmpРѓђрхЇ =
  cmpРѓђ╦б-hРѓђ
comРѓѓ (Map.idn (cmpРѓђрхЇ {B = B}{C}) {g , _}) =
  S.cmpрхЌ (G.hom╦б C _)
    ( Map.idn g
    , G.idn-rhs C (g Map.$РѓЂ (G.idn╦б B S.Map.$Рѓђ T.­ЮЪЎ.*)) )
comРѓѓ (Map.cmp (cmpРѓђрхЇ {C = C}) {c = hРѓЂ , _} (╬▓РѓЂ , _) _) =
  S.cmpрхЌ (G.hom╦б C _)
    ( S.cmpрхЌ (G.hom╦б C _)
      ( S.cmpрхЌ (G.hom╦б C _)
        ( S.invрхЌ (G.hom╦б C _) (G.cmp-ass C _ _ _)
        , G.cmp╦б C S.Map.$РѓЂ
          ( S.idnрхЌ (G.hom╦б C _) _
          , S.cmpрхЌ (G.hom╦б C _)
            ( S.cmpрхЌ (G.hom╦б C _)
              ( G.cmp-ass C _ _ _
              , G.cmp╦б C S.Map.$РѓЂ
                ( S.invрхЌ (G.hom╦б C _) (natРѓЂ ╬▓РѓЂ _)
                , S.idnрхЌ (G.hom╦б C _) _ ) )
            , S.invрхЌ (G.hom╦б C _) (G.cmp-ass C _ _ _) ) ) )
      , G.cmp-ass C _ _ _ )
    , G.cmp╦б C S.Map.$РѓЂ (Map.cmp hРѓЂ _ _ , S.idnрхЌ (G.hom╦б C _) _) )

!рхЇ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ G.obj A Рєњ (B Map.РЄњРѓђрхЌ A)
Map._$Рѓђ_ (!рхЇ a) _ = a
Map.-$РѓЂ╦б- (!рхЇ {A = A} a) = S.Map.!╦б (G.idn╦б A S.Map.$Рѓђ _)
Map.idn (!рхЇ {A = A} a) = S.idnрхЌ (G.hom╦б A _) _
Map.cmp (!рхЇ {A = A} a) g f = S.invрхЌ (G.hom╦б A _) (G.idn-rhs A (G.idn╦б A S.Map.$Рѓђ _))

curry
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░ РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░ РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ {A : G.t d РёЊРѓђрхњ РёЊРѓђ╦брхњ РёЊРѓђ╦б╩░}
  Рєњ {B : G.t d РёЊРѓЂрхњ РёЊРѓЂ╦брхњ РёЊРѓЂ╦б╩░}
  Рєњ {C : G.t d РёЊРѓѓрхњ РёЊРѓѓ╦брхњ РёЊРѓѓ╦б╩░}
  Рєњ (F : (A Ten.РіЌ B) Map.РЄњРѓђрхЌ C)
  Рєњ A Map.РЄњРѓђрхЌ (B РЄњРѓђрхЇ C)
curry {A = A}{B = B}{C = C} F = record
  { _$Рѓђ_ = ╬╗ a Рєњ record
    { _$Рѓђ_ = ╬╗ b Рєњ F Map.$Рѓђ (a , b)
    ; -$РѓЂ╦б- = ╬╗ {bРѓђ}{bРѓЂ} Рєњ record
      { _$Рѓђ_ = ╬╗ f Рєњ F Map.$РѓЂ (G.idnРѓђ A , f)
      ; _$РѓЂ_ = ╬╗ {fРѓђ}{fРѓЂ} p Рєњ F Map.$Рѓѓ (S.idnрхЌ (G.hom╦б A _) _ , p) }
    ; idn = ╬╗ {b} Рєњ Map.idn F
    ; cmp = ╬╗ {bРѓђ}{bРѓЂ}{bРѓѓ} g f Рєњ
      S.cmpрхЌ (G.hom╦б C _)
        ( Map.cmp F _ _
        , F Map.$Рѓѓ (S.invрхЌ (G.hom╦б A _) (G.idn-rhs A (G.idnРѓђ A)) , S.idnрхЌ (G.hom╦б B _) _) )
    }
  ; -$РѓЂ╦б- = ╬╗ {aРѓђ}{aРѓЂ} Рєњ record
    { _$Рѓђ_ = ╬╗ f Рєњ record
      { comРѓЂ = ╬╗ {b} Рєњ F Map.$РѓЂ (f , G.idnРѓђ B)
      ; natРѓЂ = ╬╗ {bРѓђ}{bРѓЂ} g Рєњ
        S.cmpрхЌ (G.hom╦б C _)
          ( S.cmpрхЌ (G.hom╦б C _)
            ( Map.cmp F _ _
            , F Map.$Рѓѓ
              ( S.cmpрхЌ (G.hom╦б A _) (S.invрхЌ (G.hom╦б A _) (G.idn-lhs A _) , G.idn-rhs A _)
              , S.cmpрхЌ (G.hom╦б B _) (S.invрхЌ (G.hom╦б B _) (G.idn-rhs B _) , G.idn-lhs B _) ) )
          , S.invрхЌ (G.hom╦б C _) (Map.cmp F _ _) )
      }
    ; _$РѓЂ_ = ╬╗ {fРѓђ}{fРѓЂ} p Рєњ record
      { comРѓѓ = ╬╗ {b} Рєњ F Map.$Рѓѓ (p , S.idnрхЌ (G.hom╦б B _) _) }
    }
  ; idn = ╬╗ {a} Рєњ record
    { comРѓѓ = ╬╗ {b} Рєњ Map.idn F }
  ; cmp = ╬╗ {aРѓђ}{aРѓЂ}{aРѓѓ} g f Рєњ record
    { comРѓѓ = ╬╗ {b} Рєњ
      S.cmpрхЌ (G.hom╦б C _)
        ( Map.cmp F _ _
        , F Map.$Рѓѓ (S.idnрхЌ (G.hom╦б A _) _ , S.invрхЌ (G.hom╦б B _) (G.idn-rhs B _)) )
    }
  }

open import Ambient.Groupoid.Map.Boot public
