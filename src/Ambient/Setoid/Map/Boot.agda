{-# OPTIONS --without-K #-}

module Ambient.Setoid.Map.Boot where

open import Agda.Primitive
import Ambient.Setoid.Base as S
open import Type as T

infixr 1 _РЄњРѓђрхЌ_
infixr 2 _РѕўРѓђрхЌ_

record _РЄњРѓђрхЌ_
  {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░ РёЊРѓЂрхњ РёЊРѓЂ╩░}
  (A : S.­ЮћіРѓЂ d РёЊРѓђрхњ РёЊРѓђ╩░)
  (B : S.­ЮћіРѓЂ d РёЊРѓЂрхњ РёЊРѓЂ╩░)
    : Set ((РёЊРѓђрхњ Ріћ РёЊРѓђ╩░) Ріћ (РёЊРѓЂрхњ Ріћ РёЊРѓЂ╩░)) where
  no-eta-equality
  infixr 4 _$Рѓђ_
  infixr 4 _$РѓЂ_
  field
    _$Рѓђ_
      : S.cellРѓђ A РЄњРѓђ,Рѓђ S.cellРѓђ B
    ._$РѓЂ_
      : Рѕђ {a b}
      Рєњ S.cellРѓЂ A (a , b) РЄњРѓђ,Рѓђ S.cellРѓЂ B (_$Рѓђ_ a , _$Рѓђ_ b)
open _РЄњРѓђрхЌ_ public

idnРѓђрхЌ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ {A : S.­ЮћіРѓЂ d РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ ­ЮЪЎРѓђ {lzero} РЄњРѓђ,Рѓђ (A РЄњРѓђрхЌ A)
_$Рѓђ_ (idnРѓђрхЌ *) = РЄњРѓђ.idnРѓђ
_$РѓЂ_ (idnРѓђрхЌ *) = РЄњРѓђ.idnРѓђ

cmpРѓђрхЌ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░ РёЊРѓЂрхњ РёЊРѓЂ╩░ РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ {A : S.­ЮћіРѓЂ d РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ {B : S.­ЮћіРѓЂ d РёЊРѓЂрхњ РёЊРѓЂ╩░}
  Рєњ {C : S.­ЮћіРѓЂ d РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ (B РЄњРѓђрхЌ C) ├ЌРѓђ (A РЄњРѓђрхЌ B) РЄњРѓђ,Рѓђ (A РЄњРѓђрхЌ C)
_$Рѓђ_ (cmpРѓђрхЌ (G , F)) = (G $Рѓђ_) РЄњРѓђ.РѕўРѓђ (F $Рѓђ_)
_$РѓЂ_ (cmpРѓђрхЌ (G , F)) = (G $РѓЂ_) РЄњРѓђ.РѕўРѓђ (F $РѓЂ_)

_РѕўРѓђрхЌ_
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░ РёЊРѓЂрхњ РёЊРѓЂ╩░ РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ {A : S.­ЮћіРѓЂ d РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ {B : S.­ЮћіРѓЂ d РёЊРѓЂрхњ РёЊРѓЂ╩░}
  Рєњ {C : S.­ЮћіРѓЂ d РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ (B РЄњРѓђрхЌ C) РЄњРѓђ,Рѓђ (A РЄњРѓђрхЌ B) РЄњРѓђ,Рѓђ (A РЄњРѓђрхЌ C)
_РѕўРѓђрхЌ_ G F = cmpРѓђрхЌ (G , F)
