{-# OPTIONS --without-K #-}

module Setoid.Core.Hom.Boot where

open import Agda.Primitive
import Setoid.Core.Base as S
open import Type as T
  using (_,_)

infixr 1 _РЄњРѓђрхЌ_
infixr 2 _РѕўРѓђрхЌ_

record _РЄњРѓђрхЌ_
  {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░ РёЊРѓЂрхњ РёЊРѓЂ╩░}
  (A : S.t d РёЊРѓђрхњ РёЊРѓђ╩░)
  (B : S.t d РёЊРѓЂрхњ РёЊРѓЂ╩░)
    : Set ((РёЊРѓђрхњ Ріћ РёЊРѓђ╩░) Ріћ (РёЊРѓЂрхњ Ріћ РёЊРѓЂ╩░)) where
  no-eta-equality
  infixr 4 _$Рѓђ_
  infixr 4 _$РѓЂ_
  field
    _$Рѓђ_
      : S.obj A T.╬а.РЄњРѓђ S.obj B
    ._$РѓЂ_
      : Рѕђ {a b}
      Рєњ S.homрхЌ A (a , b) T.╬а.РЄњРѓђ S.homрхЌ B (_$Рѓђ_ a , _$Рѓђ_ b)
open _РЄњРѓђрхЌ_ public

idnРѓђрхЌ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ {A : S.t d РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ T.­ЮЪЎ.tРЂ░ T.╬а.РЄњРѓђ (A РЄњРѓђрхЌ A)
_$Рѓђ_ (idnРѓђрхЌ T.­ЮЪЎ.*) = T.╬а.idn
_$РѓЂ_ (idnРѓђрхЌ T.­ЮЪЎ.*) = T.╬а.idn

cmpРѓђрхЌ
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░ РёЊРѓЂрхњ РёЊРѓЂ╩░ РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ {A : S.t d РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ {B : S.t d РёЊРѓЂрхњ РёЊРѓЂ╩░}
  Рєњ {C : S.t d РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ (B РЄњРѓђрхЌ C) T.Рѕљ.РіЌ (A РЄњРѓђрхЌ B) T.╬а.РЄњРѓђ (A РЄњРѓђрхЌ C)
_$Рѓђ_ (cmpРѓђрхЌ (G , F)) = (G $Рѓђ_) T.╬а.Рѕў (F $Рѓђ_)
_$РѓЂ_ (cmpРѓђрхЌ (G , F)) = (G $РѓЂ_) T.╬а.Рѕў (F $РѓЂ_)

_РѕўРѓђрхЌ_
  : Рѕђ {d} ..{РёЊРѓђрхњ РёЊРѓђ╩░ РёЊРѓЂрхњ РёЊРѓЂ╩░ РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ {A : S.t d РёЊРѓђрхњ РёЊРѓђ╩░}
  Рєњ {B : S.t d РёЊРѓЂрхњ РёЊРѓЂ╩░}
  Рєњ {C : S.t d РёЊРѓѓрхњ РёЊРѓѓ╩░}
  Рєњ (B РЄњРѓђрхЌ C) T.╬а.РЄњРѓђ (A РЄњРѓђрхЌ B) T.╬а.РЄњРѓђ (A РЄњРѓђрхЌ C)
_РѕўРѓђрхЌ_ G F = cmpРѓђрхЌ (G , F)
